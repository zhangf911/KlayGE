set(ZLIB_PROJECT_DIR "${EXTERNAL_PROJECT_DIR}/zlib")
set(ZLIB_OUTPUT_DIR "${EXTERNAL_PROJECT_DIR}/lib/zlib/${KLAYGE_PLATFORM_NAME}")

UpdateExternalLib("zlib" "https://github.com/madler/zlib.git" "cacf7f1d4e3d44d871b605da3b647f07d718623f")

set(SKIP_INSTALL_ALL ON CACHE BOOL "" FORCE)
set(AMD64 OFF CACHE BOOL "" FORCE)
set(ASM686 OFF CACHE BOOL "" FORCE)
set(LIBRARY_OUTPUT_PATH CACHE STRING "${ZLIB_OUTPUT_DIR}" FORCE)

add_subdirectory(${ZLIB_PROJECT_DIR} "${CMAKE_CURRENT_BINARY_DIR}/zlib")

if(KLAYGE_PREFERRED_LIB_TYPE STREQUAL "STATIC")
	set(KLAYGE_ZLIB_NAME "zlibstatic" CACHE STRING "" FORCE)
else()
	set(KLAYGE_ZLIB_NAME "zlib" CACHE STRING "" FORCE)
endif()

set(CMAKE_DISABLE_FIND_PACKAGE_ZLIB TRUE CACHE BOOL "" FORCE)
set(ZLIB_INCLUDE_DIR
	${EXTERNAL_PROJECT_DIR}/zlib
	${PROJECT_BINARY_DIR}/External/zlib/zlib
	CACHE INTERNAL "" FORCE
)
set(ZLIB_INCLUDE_DIRS ${ZLIB_INCLUDE_DIR} CACHE INTERNAL "" FORCE)
set(ZLIB_LIBRARY zlib CACHE INTERNAL "" FORCE)
set(ZLIB_LIBRARIES ${ZLIB_LIBRARY} CACHE INTERNAL "" FORCE)
set(ZLIB_FOUND TRUE CACHE INTERNAL "" FORCE)

set_target_properties(${KLAYGE_ZLIB_NAME} PROPERTIES
	ARCHIVE_OUTPUT_DIRECTORY ${ZLIB_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${ZLIB_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${ZLIB_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${ZLIB_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${ZLIB_OUTPUT_DIR}
	RUNTIME_OUTPUT_DIRECTORY ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_BIN_DIR}
	PROJECT_LABEL ${KLAYGE_ZLIB_NAME}
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME ${KLAYGE_ZLIB_NAME}${KLAYGE_OUTPUT_SUFFIX}
)

foreach(target
	"example" "minigzip" "zlib" "zlibstatic")
	set_target_properties(${target} PROPERTIES FOLDER "${EXTERNAL_VS_FOLDER_PREFIX}/zlib")
endforeach()
